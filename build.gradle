buildscript {
	repositories {
		mavenLocal()
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath 'org.ajoberstar:gradle-git:latest.integration'
	}
}

apply plugin: 'grgit-release'
apply plugin: 'java'

import org.ajoberstar.grgit.*

release {
	grgit = Grgit.open(project.file('.'))
	releaseTasks = ['build']
	enforceSinceTags = true
	generateTagMessage = { version ->
		StringBuilder builder = new StringBuilder()
		builder.append('Release of ')
		builder.append(version)
		builder.append('\n\n')
		grgit.log {
			range version.nearest.normal.toString(), 'HEAD'
		}.inject(builder) { bldr, commit ->
			bldr.append('- ')
			bldr.append(commit.shortMessage)
			bldr.append('\n')
		}
		builder.toString()
	}
}

wrapper {
	gradleVersion = '1.11'
}
